Sub メインモジュール()

Dim v成績表 As c成績表
Set v成績表 = New c成績表


Dim 成績表シート As Worksheet
Set 成績表シート = ThisWorkbook.Worksheets("成績表")  '対象シート名が変更された場合""内を修正

Set v成績表.対象シート = 成績表シート



Dim 末尾行 As Long
末尾行 = 成績表シート.Cells(成績表シート.Rows.Count, "B").End(xlUp).Row

Call v成績表.セル範囲設定(2, 末尾行, 2, 6)
Call v成績表.対象範囲にのセル色を初期色にする
Call v成績表.対象範囲にのセル色を赤にする

Set v成績表 = Nothing



End Sub


Option Explicit

Private m対象シート As Worksheet
Private mセル範囲 As String
Public Property Set 対象シート(p対象シート As Worksheet)
  Set m対象シート = p対象シート
End Property
Public Function セル範囲設定(p開始行 As Long, p末尾行 As Long, p開始列 As Long, p末尾列 As Long)
    mセル範囲 = m対象シート.Range(m対象シート.Cells(p開始行, p開始列), m対象シート.Cells(p末尾行, p末尾列)).Address
End Function
Public Sub 対象範囲にのセル色を初期色にする()
    m対象シート.Range(mセル範囲).Interior.ColorIndex = xlNone
End Sub
Public Sub 対象範囲にのセル色を赤にする()
    Dim vセルアドレス As Variant
    For Each vセルアドレス In 該当セル一覧(80)
        m対象シート.Range(vセルアドレス).Interior.Color = RGB(255, 0, 0)
    Next vセルアドレス
End Sub
Private Function 該当セル一覧(p点数 As Long) As Collection
    Dim セル一覧 As Collection
    Set セル一覧 = New Collection

    Dim vRange As Range
    For Each vRange In m対象シート.Range(mセル範囲)
       If vRange.Value > p点数 Then
       セル一覧.Add vRange.Address
       End If
    Next vRange
    
    Set 該当セル一覧 = セル一覧
End Function
